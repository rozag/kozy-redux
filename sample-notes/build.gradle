apply plugin: "com.android.application"
apply plugin: "kotlin-android"

android {
    compileSdkVersion compileSdk
    buildToolsVersion buildTools

    defaultConfig {
        applicationId "com.github.rozag.redux.notes"

        minSdkVersion minSdk
        targetSdkVersion targetSdk

        versionCode majorAppVersion * 100 + minorAppVersion
        versionName "${majorAppVersion}.${minorAppVersion}"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            debuggable true
            jniDebuggable true
            renderscriptDebuggable true
            pseudoLocalesEnabled false
            zipAlignEnabled true
            applicationIdSuffix ".debug"
        }

        release {
            minifyEnabled true
            debuggable false
            jniDebuggable false
            renderscriptDebuggable false
            pseudoLocalesEnabled false
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
        }
    }

    lintOptions {
        textReport true
        textOutput "stdout"
        fatal "UnusedResources"
        warningsAsErrors true
        abortOnError true
    }

    compileOptions {
        sourceCompatibility sourceCompatJavaVersion
        targetCompatibility targetCompatJavaVersion
    }

    packagingOptions {
        exclude "META-INF/NOTICE"
        exclude "META-INF/NOTICE.txt"
        exclude "META-INF/LICENSE"
        exclude "META-INF/LICENSE.txt"
        exclude "META-INF/services/javax.annotation.processing.Processor"
    }

    dexOptions {
        javaMaxHeapSize "3g"
        jumboMode = true
    }

    sourceSets {
        main.java.srcDirs += "src/main/kotlin"
        androidTest.java.srcDirs += "src/androidTest/kotlin"
        test.java.srcDirs += "src/test/kotlin"
    }
}

dependencies {
//    implementation project(':libcore')
//    implementation project(':libbase')
    implementation "com.github.rozag:kozy-redux-base:${publishVersion}"

    // Kotlin specific dependencies
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:${kotlinVersion}"

    // Android support library
    implementation "com.android.support:appcompat-v7:${supportLibraryVersion}"

    // Timber logging library
    implementation "com.jakewharton.timber:timber:${timberVersion}"

    // Room
    implementation "android.arch.persistence.room:runtime:${roomVersion}"
    kapt "android.arch.persistence.room:compiler:${roomVersion}"
}